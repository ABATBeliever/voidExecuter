#module
	#deffunc ListJsonBreak str p3
		WriteLog "ListJsonBreak *"+p3
		p1=""+AppName@+" - 警告"
		p2="ランチャーリストが破損しています。"
		p4="'初期化'を選択した場合、既定が復元されますが、設定は消えます。\n'終了'を選択するかこのポップアップを閉じた場合、"+AppName@+"は終了します。"
		p5="初期化\n終了"
		p6=2
		p7=1
		DialogVistaEx p1,p2,p3,p4,p5,p6,p7
		switch stat
			case 101 : WriteLog "List Reset" : bcopy "include/default.json","list.json" : dialog "既定値を復元しました。再起動してください。",0,AppName@+" - 情報" : end
			default  : WriteLog "Exit"       : end
		swend
	return
#global

if IsNotExist("list.json") {
	WriteLog "Create List"
	bcopy "include/default.json","list.json"
}

WriteLog "Load list.json"
notesel JsonData
noteload "list.json"
jsonopen JsonPtr,JsonData

jsongeti JsonVersion,"version",JsonPtr
if stat!=0        {ListJsonBreak "Jsonのバージョンを把握できません。"}
if JsonVersion!=1 {ListJsonBreak "Jsonのバージョンが不正です。\nJsonに記載されたバージョン: "+JsonVersion+"\nシステムが要求するバージョン: 1\nJsonファイルのversionを修正することで治る可能性があります。"}

jsongetobj InfoPtr,"entries",JsonPtr
if stat!=0        {ListJsonBreak "Jsonのランチャーエントリが破損しています。\nこれは致命的です。続行できません。":end}

jsonnext InfoPtr,InfoPtr,2
while(InfoPtr!=NULL)
	jsongets InfoType(count)      ,"type",    InfoPtr
	jsongets InfoLabel(count)     ,"label",   InfoPtr
	switch InfoType(count)
		case "url"
			jsongets Info1(count) ,"url",     InfoPtr
			jsongets info2(count) ,"browser", InfoPtr
			swbreak
		case "app"
			jsongets Info1(count) ,"path",    InfoPtr
			jsongets Info2(count) ,"args",    InfoPtr
			swbreak
		case "file"
			jsongets Info1(count) ,"path",    InfoPtr
			jsongets Info2(count) ,"cliant",  InfoPtr
			swbreak
		case "script"
			jsongets Info1(count),"path",     InfoPtr
			jsongets Info2(count),"console",  InfoPtr
			swbreak
		case "null"
			InfoLabel(count)="定義された空白"
			Info1(count)    ="Invalid"
			Info2(count)    ="Invalid"
			swbreak
		default
			InfoType(count) ="Invalid"
			InfoLabel(count)="破損"
			Info1(count)    ="Invalid"
			Info2(count)    ="Invalid"
			swbreak
	swend
	jsonnext InfoPtr,InfoPtr,0
	count++
wend

repeat count
	if InfoType(cnt)="Invalid" or InfoType(cnt)="" : ListJsonBreak "Jsonのランチャーエントリが破損しています。\n破損したID: "+str(cnt)+"\n内容:typeデータの破損"
	if InfoType(cnt)="app"     or InfoType(cnt)="file" {
		exist Info1(cnt)
		if strsize=-1 {
			dialog "アプリまたはファイルが見つかりませんでした。\nID:"+str(cnt)+"\nファイルパス:"+Info1(cnt),1,AppName+" - 警告"
			InfoType(cnt) ="null"
			InfoLabel(cnt)="見つからなかった事による空白"
			Info1(cnt)    ="Invalid"
			Info2(cnt)    ="Invalid"
		}
	}
	if Infolabel(cnt)=""  : Infolabel(cnt)="破損"  : ListJsonBreak "Jsonのランチャーエントリが破損しています。\n破損したID: "+str(cnt)+"\n内容:laberの破損"
	if Info1(cnt)=""                               : ListJsonBreak "Jsonのランチャーエントリが破損しています。\n破損したID: "+str(cnt)+"\n内容:第3データの破損"
	//Info2は空白を許す
loop
WriteLog "Load List SUCSESS"